plugins {
    id 'fabric-loom' version '1.10-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    accessWidenerPath = file("src/main/resources/blade.accesswidener")
}

repositories {
    mavenCentral()
    maven {
        name = "Fabric"
        url = "https://maven.fabricmc.net/"
    }
    maven {
        name = "ViaVersion"
        url = "https://repo.viaversion.com"
    }
    maven {
        name = "OpenCollab"
        url = "https://repo.opencollab.dev/maven-snapshots"
    }
    maven {
        url = 'https://jitpack.io'
    }
}

dependencies {
    modImplementation "org.reflections:reflections:0.10.2"
    include "org.reflections:reflections:0.10.2"

    implementation 'io.github.CDAGaming:DiscordIPC:0.10.2'

    implementation 'io.github.earcut4j:earcut4j:2.2.2'
    include 'io.github.earcut4j:earcut4j:2.2.2'
    def batikVersion = '1.17'
    def batikExclude = {
        exclude group: 'xalan', module: 'xalan'
        exclude group: 'xml-apis', module: 'xml-apis'
    }


    modImplementation "dev.redstones.mediaplayerinfo:media-player-info:0.1.0"

    implementation("org.apache.xmlgraphics:batik-parser:$batikVersion", batikExclude)
    implementation("org.apache.xmlgraphics:batik-awt-util:$batikVersion", batikExclude)
    implementation("org.apache.xmlgraphics:batik-transcoder:$batikVersion", batikExclude)

    implementation("org.apache.xmlgraphics:batik-codec:$batikVersion", batikExclude)


    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation 'com.github.CCBlueX:mcef:3.1.0-1.21.4'
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    repositories {
    }
}